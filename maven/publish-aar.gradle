/*
âš ï¸ä¸Šä¼ aaråˆ°ç§æœ‰mavenï¼Œæ ¹æ®maven_is_snapshotå†³å®šä¸Šä¼ åˆ°snapshotè¿˜æ˜¯releaseä»“åº“

ğŸš€ğŸš€ğŸš€
1. å°†ä»¥ä¸‹ä»£ç copyåˆ°éœ€è¦è¢«ä¸Šä¼ çš„moduleçš„build.gradleä¸­
2. ç¼–è¯‘ååœ¨IDEä¾§è¾¹gradleè§†çª—:${Module}/Tasks/publishing/ï¼Œæ‰§è¡Œpublish
ğŸš€ğŸš€ğŸš€

ext {
    maven_is_snapshot = true
    maven_artifact_file_path = "./xxx.aar"
    maven_build_type = "debug" // debugã€releaseã€preã€alpha
    maven_version = "x.x.x"
    maven_artifactId = "åå­—"
    maven_description = "æè¿°"
    maven_groupId = "com.joyratel.android"
}

apply from: "${rootProject.rootDir}/gradle-script/maven/publish-aar.gradle"

*/

// â†‘ â†‘ â†‘ â†‘ â†‘ â†‘ â†‘ â†‘ â†‘ â†‘ ä½¿ç”¨æ–¹å¼ â†‘ â†‘ â†‘ â†‘ â†‘ â†‘ â†‘ â†‘ â†‘ â†‘ â†‘ â†‘ â†‘


apply plugin: 'maven-publish'

publishing {
    publications {
        "${maven_build_type}"(MavenPublication) {
            groupId maven_groupId
            artifactId maven_artifactId
            // 0.0.1-debug-snapshot ä¸å«buildTypeï¼Œé€‚ç”¨äºè‡ªæœ‰moduleï¼Œéœ€è¦ä¸åŒçš„buildType
            version maven_version + "-" + maven_build_type + (maven_is_snapshot ? "-SNAPSHOT" : "")
            artifact maven_artifact_file_path

            // To include project dependencies
            pom.withXml {
                def dependencies = asNode().appendNode('dependencies')
                // maven_build_type æ ¹æ®build_typeä¸Šä¼ ç›¸åº”çš„dependencies.
                // å¦‚debugImplementationã€releaseImplementationç­‰ï¼Œæ ¹æ®buildType
                configurations.getByName("${maven_build_type}CompileClasspath").getResolvedConfiguration().getFirstLevelModuleDependencies().each {
                    def dependency = dependencies.appendNode('dependency')
                    dependency.appendNode('groupId', it.moduleGroup)
                    dependency.appendNode('artifactId', it.moduleName)
                    dependency.appendNode('version', it.moduleVersion)
                }
                asNode().appendNode("description", maven_description)
            }
        }
    }
    repositories {
        maven {
            if (maven_is_snapshot) {
                url maven_snapshots_url
            } else {
                url maven_releases_url
            }
            credentials(HttpHeaderCredentials) {
                name = "Private-Token"
                if (maven_is_snapshot) {
                    value = maven_snapshots_gitLabAccessToken
                } else {
                    value = maven_releases_gitLabAccessToken
                }
            }
            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }
}


